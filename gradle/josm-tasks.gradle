// Custom tasks for running JOSM with the plugin loaded

def tmpJosmHome = "$buildDir/.josm"

task cleanJosm(type: Delete) {
  group 'JOSM'
  description 'Delete JOSM configuration in `build/.josm/`'
  delete tmpJosmHome
}

task initJosmPrefs(type: Copy) {
  description 'Puts a default preferences.xml file into `build/.josm/`'
  doFirst {
    if (new File("$tmpJosmHome/preferences.xml").exists()) {
      println "JOSM preferences not copied, file is already present.\nIf you want to replace it, run the task 'cleanJosm' additionally."
      exclude '*'
    }
  }
  from "config/josm/preferences.xml"
  into "$buildDir/.josm"
}

task updateJosmPlugin(type: Copy) {
  description 'Puts the plugin-JAR generated by the `jar`-task into `build/.josm/`'
  dependsOn jar
  dependsOn initJosmPrefs
  from configurations.requiredPlugin
  from "$buildDir/libs/${project.name}.jar"
  into "$tmpJosmHome/plugins"
  rename('(.*)-\\.jar', '$1.jar')
  rename(project.name, project.hasProperty('plugin.jar.name') ? project.property('plugin.jar.name') : project.name)
}

task runJosm(type: RunJosmTask) {
  description 'Runs an independent JOSM instance (version specified in project dependencies) with `build/.josm/` as home directory and the freshly compiled Mapillary plugin active.'
}

task debugJosm(type: RunJosmTask) {
  description 'Runs a JOSM instance like the task `runJosm`, but with JDWP (Java debug wire protocol) active on port 7051'
  extraInformation '\nThe application is listening for a remote debugging connection on port 7051. It will start execution as soon as the debugger is connected.\n'
  jvmArgs "-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=7051"
}

/**
 * Define a RunJosmTask as subclass of JavaExec
 */
class RunJosmTask extends JavaExec {
  String extraInformation = ''
  public RunJosmTask() {
    group 'JOSM'
    main 'JOSM'
    args (project.hasProperty('josmArgs') ? project.josmArgs.split('\\\\') : [])
  }
}

/**
 * Extend all RunJosmTasks with extra functionality
 */
tasks.withType(RunJosmTask) {
  dependsOn updateJosmPlugin
  classpath = sourceSets.main.runtimeClasspath
  systemProperties['josm.home'] = tmpJosmHome
  doFirst {
    println 'Starting JOSM ' + getVersionName()

    println '\nThese system properties are set:'
    for (def entry : systemProperties.entrySet()) {
      println entry.key + " = " + entry.value
    }

    if (args.size() <= 0) {
      println '\nNo command line arguments are passed to JOSM.\nIf you want to pass arguments to JOSM add \'-PjosmArgs="arg0\\\\arg1\\\\arg2\\\\..."\' when starting Gradle from the commandline (separate the arguments with double-backslashes).'
    } else {
      println '\nPassing these arguments to JOSM:'
      println args.join('\n')
    }

    print extraInformation

    println '\nOutput of JOSM starts after the three equality signs\n==='
  }
}
